# - Find the CPLEX LP solver.
# This code defines the following variables:
#
#  CPLEX_STANDALONE_FOUND                 - TRUE if CPLEX was found.
#  CPLEX_STANDALONE_INCLUDE_DIRS          - Full paths to all include dirs.
#  CPLEX_STANDALONE_LIBRARIES             - Full paths to all libraries.
#  CPLEX_STANDALONE_RUNTIME_LIBRARY       - Full path to the dll file on windows
#
# Usage:
#  find_package(cplexstandalone)
#
# The location of CPLEX can be specified using the environment variable
# or cmake parameter DOWNWARD_CPLEX_ROOT. The same can be done for the concert
# component via DOWNWARD_CPLEX_CONCERT. If different installations
# for 32-/64-bit versions and release/debug versions of CPLEX are available,
# they can be specified with
#   DOWNWARD_CPLEX_{ROOT,CONCERT}32
#   DOWNWARD_CPLEX_{ROOT,CONCERT}64
#   DOWNWARD_CPLEX_{ROOT,CONCERT}_RELEASE32
#   DOWNWARD_CPLEX_{ROOT,CONCERT}_RELEASE64
#   DOWNWARD_CPLEX_{ROOT,CONCERT}_DEBUG32
#   DOWNWARD_CPLEX_{ROOT,CONCERT}_DEBUG64
# More specific paths are preferred over less specific ones when searching
# for libraries.
#
# Note that the standard FIND_PACKAGE features are supported
# (QUIET, REQUIRED, etc.).

foreach(BITWIDTH 32 64)
    foreach(BUILDMODE "RELEASE" "DEBUG")
        set(CPLEX_STANDALONE_HINT_PATHS_${BUILDMODE}${BITWIDTH}
            ${DOWNWARD_CPLEX_ROOT_${BUILDMODE}${BITWIDTH}}
            $ENV{DOWNWARD_CPLEX_ROOT_${BUILDMODE}${BITWIDTH}}
            ${DOWNWARD_CPLEX_ROOT${BITWIDTH}}
            $ENV{DOWNWARD_CPLEX_ROOT${BITWIDTH}}
            ${DOWNWARD_CPLEX_ROOT}
            $ENV{DOWNWARD_CPLEX_ROOT}
        )
        set(CPLEX_CONCERT_HINT_PATHS_${BUILDMODE}${BITWIDTH}
            ${DOWNWARD_CPLEX_CONCERT_${BUILDMODE}${BITWIDTH}}
            $ENV{DOWNWARD_CPLEX_CONCERT_${BUILDMODE}${BITWIDTH}}
            ${DOWNWARD_CPLEX_CONCERT${BITWIDTH}}
            $ENV{DOWNWARD_CPLEX_CONCERT${BITWIDTH}}
            ${DOWNWARD_CPLEX_CONCERT}
            $ENV{DOWNWARD_CPLEX_CONCERT}
        )
    endforeach()
endforeach()

if(${CMAKE_SIZEOF_VOID_P} EQUAL 4)
    set(CPLEX_STANDALONE_HINT_PATHS_RELEASE ${CPLEX_STANDALONE_HINT_PATHS_RELEASE32})
    set(CPLEX_STANDALONE_HINT_PATHS_DEBUG ${CPLEX_STANDALONE_HINT_PATHS_DEBUG32})
    
    set(CPLEX_CONCERT_HINT_PATHS_RELEASE ${CPLEX_CONCERT_HINT_PATHS_RELEASE32})
    set(CPLEX_CONCERT_HINT_PATHS_DEBUG ${CPLEX_CONCERT_HINT_PATHS_DEBUG32})
elseif(${CMAKE_SIZEOF_VOID_P} EQUAL 8)
    set(CPLEX_STANDALONE_HINT_PATHS_RELEASE ${CPLEX_STANDALONE_HINT_PATHS_RELEASE64})
    set(CPLEX_STANDALONE_HINT_PATHS_DEBUG ${CPLEX_STANDALONE_HINT_PATHS_DEBUG64})
    
    set(CPLEX_CONCERT_HINT_PATHS_RELEASE ${CPLEX_CONCERT_HINT_PATHS_RELEASE64})
    set(CPLEX_CONCERT_HINT_PATHS_DEBUG ${CPLEX_CONCERT_HINT_PATHS_DEBUG64})
else()
    message(WARNING "Bitwidth could not be detected, preferring 32-bit version of CPLEX")
    set(CPLEX_STANDALONE_HINT_PATHS_RELEASE
        ${CPLEX_STANDALONE_HINT_PATHS_RELEASE32}
        ${CPLEX_STANDALONE_HINT_PATHS_RELEASE64}
    )
    set(CPLEX_STANDALONE_HINT_PATHS_DEBUG
        ${CPLEX_STANDALONE_HINT_PATHS_DEBUG32}
        ${CPLEX_STANDALONE_HINT_PATHS_DEBUG64}
    )
    
    set(CPLEX_CONCERT_HINT_PATHS_RELEASE
        ${CPLEX_CONCERT_HINT_PATHS_RELEASE32}
        ${CPLEX_CONCERT_HINT_PATHS_RELEASE64}
    )
    set(CPLEX_CONCERT_HINT_PATHS_DEBUG
        ${CPLEX_CONCERT_HINT_PATHS_DEBUG32}
        ${CPLEX_CONCERT_HINT_PATHS_DEBUG64}
    )
endif()

find_path(CPLEX_STANDALONE_INCLUDE_DIRS
    NAMES
    ilcplex/cplex.h
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_RELEASE}
    ${CPLEX_STANDALONE_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    include
)

find_path(CPLEX_CONCERT_INCLUDE_DIRS
    NAMES
    ilconcert/iloenv.h
    HINTS
    ${CPLEX_CONCERT_HINT_PATHS_RELEASE}
    ${CPLEX_CONCERT_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    include
)

if(APPLE)
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32
        "lib/x86_osx/static_pic")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32 ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32})
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64
        "lib/x86-64_osx/static_pic")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64 ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64})
elseif(UNIX)
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32
        "lib/x86_sles10_4.1/static_pic"
        "lib/x86_linux/static_pic")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32 ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32})
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64
        "bin/x86-64_linux/"
        "lib/x86-64_sles10_4.1/static_pic"
        "lib/x86-64_linux/static_pic")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64 ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64})
    
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_32
        "lib/x86_sles10_4.1/static_pic"
        "lib/x86_linux/static_pic")
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_32 ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_32})
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_64
        "lib/x86-64_sles10_4.1/static_pic"
        "lib/x86-64_linux/static_pic")
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_64 ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_64})
elseif(MSVC)
    # Note that the numbers are correct: Visual Studio 2011 is version 10.
    if (MSVC10)
        set(CPLEX_STANDALONE_COMPILER_HINT "vs2011")
    elseif(MSVC11)
        set(CPLEX_STANDALONE_COMPILER_HINT "vs2012")
    elseif(MSVC12)
        set(CPLEX_STANDALONE_COMPILER_HINT "vs2013")
    elseif(MSVC13)
        set(CPLEX_STANDALONE_COMPILER_HINT "vs2015")
    elseif(MSVC14)
        set(CPLEX_STANDALONE_COMPILER_HINT "vs2017")
    endif()

    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32 "lib/x86_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mda")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32 "lib/x86_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mdd")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64
      "lib/x86-64_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mda"
      "lib/x64_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mda")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64
      "lib/x86-64_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mdd"
      "lib/x64_windows_${CPLEX_STANDALONE_COMPILER_HINT}/stat_mdd")

    if(${CMAKE_SIZEOF_VOID_P} EQUAL 4)
        set(CPLEX_STANDALONE_RUNTIME_LIBRARY_HINT "bin/x86_win32")
    elseif(${CMAKE_SIZEOF_VOID_P} EQUAL 8)
        set(CPLEX_STANDALONE_RUNTIME_LIBRARY_HINT "bin/x64_win64")
    endif()
endif()

if(${CMAKE_SIZEOF_VOID_P} EQUAL 4)
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32})
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32})
    
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_32})
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_32})
elseif(${CMAKE_SIZEOF_VOID_P} EQUAL 8)
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64})
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64})
    
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_64})
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_64})
else()
    message(WARNING "Bitwidth could not be detected, preferring 32bit version of CPLEX")
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE
        ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32}
        ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64}
    )
    set(CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG
        ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32}
        ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64}
    )
    
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE
        ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_32}
        ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE_64}
    )
    set(CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG
        ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_32}
        ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG_64}
    )
endif()

# CMake uses the first discovered library, searching in the order they
# are mentioned here. We prefer dynamic libraries over static ones
# (see issue925) and otherwise prefer the latest available version.
find_library(CPLEX_STANDALONE_LIBRARY_RELEASE
    NAMES
    cplex1210
    cplex1290
    cplex1280
    cplex1271
    cplex1262
    cplex
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_RELEASE}
    PATH_SUFFIXES
    ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE}
)

# See above.
find_library(CPLEX_STANDALONE_LIBRARY_DEBUG
    NAMES
    cplex1210
    cplex1290
    cplex1280
    cplex1271
    cplex1262
    cplex
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG}
)

find_library(CPLEX_CONCERT_LIBRARY_RELEASE
    NAMES
    concert
    HINTS
    ${CPLEX_CONCERT_HINT_PATHS_RELEASE}
    PATH_SUFFIXES
    ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_RELEASE}
)

# See above.
find_library(CPLEX_CONCERT_LIBRARY_DEBUG
    NAMES
    concert
    HINTS
    ${CPLEX_CONCERT_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    ${CPLEX_CONCERT_LIBRARY_PATH_SUFFIX_DEBUG}
)

find_library(CPLEX_ILO_LIBRARY_RELEASE
    NAMES
    ilocplex
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_RELEASE}
    PATH_SUFFIXES
    ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE}
)

find_library(CPLEX_ILO_LIBRARY_DEBUG
    NAMES
    ilocplex
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    ${CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG}
)

# Parse CPLEX version.
if(CPLEX_STANDALONE_INCLUDE_DIRS)
    file(STRINGS ${CPLEX_STANDALONE_INCLUDE_DIRS}/ilcplex/cpxconst.h CPLEX_STANDALONE_VERSION_STR
         REGEX "#define[ ]+CPX_VERSION[ ]+[0-9]+")
    string(REGEX MATCH "[0-9]+" CPLEX_STANDALONE_VERSION_STR ${CPLEX_STANDALONE_VERSION_STR})
endif()
if(CPLEX_STANDALONE_VERSION_STR)
    math(EXPR CPLEX_STANDALONE_VERSION_MAJOR "${CPLEX_STANDALONE_VERSION_STR} / 1000000")
    math(EXPR CPLEX_STANDALONE_VERSION_MINOR "${CPLEX_STANDALONE_VERSION_STR} / 10000 % 100")
    math(EXPR CPLEX_STANDALONE_VERSION_SUBMINOR "${CPLEX_STANDALONE_VERSION_STR} / 100 % 100")
    set(CPLEX_STANDALONE_VERSION
        "${CPLEX_STANDALONE_VERSION_MAJOR}.${CPLEX_STANDALONE_VERSION_MINOR}.${CPLEX_STANDALONE_VERSION_SUBMINOR}")
endif()

if(CPLEX_STANDALONE_LIBRARY_RELEASE OR CPLEX_STANDALONE_LIBRARY_DEBUG)
    find_package(Threads REQUIRED)

    set(CPLEX_STANDALONE_LIBRARIES_COMMON ${CMAKE_THREAD_LIBS_INIT})
    if(NOT (${CPLEX_STANDALONE_VERSION} VERSION_LESS "12.8"))
        set(CPLEX_STANDALONE_LIBRARIES_COMMON ${CPLEX_STANDALONE_LIBRARIES_COMMON} ${CMAKE_DL_LIBS})
    endif()

    set(CPLEX_STANDALONE_LIBRARIES
        optimized ${CPLEX_STANDALONE_LIBRARY_RELEASE} ${CPLEX_STANDALONE_LIBRARIES_COMMON}
        debug ${CPLEX_STANDALONE_LIBRARY_DEBUG} ${CPLEX_STANDALONE_LIBRARIES_COMMON}
    )
    
    set(CPLEX_CONCERT_LIBRARIES
        optimized ${CPLEX_CONCERT_LIBRARY_RELEASE}
        debug ${CPLEX_CONCERT_LIBRARY_DEBUG}
    )
    
    set(CPLEX_ILO_LIBRARIES
        optimized ${CPLEX_ILO_LIBRARY_RELEASE}
        debug ${CPLEX_ILO_LIBRARY_DEBUG}
    )
endif()

# HACK: there must be a better way to find the dll file.
find_path(CPLEX_STANDALONE_RUNTIME_LIBRARY_PATH
    NAMES
    cplex1262.dll
    HINTS
    ${CPLEX_STANDALONE_HINT_PATHS_RELEASE}
    ${CPLEX_STANDALONE_HINT_PATHS_DEBUG}
    PATH_SUFFIXES
    ${CPLEX_STANDALONE_RUNTIME_LIBRARY_HINT}
)
if(CPLEX_STANDALONE_RUNTIME_LIBRARY_PATH)
    set(CPLEX_STANDALONE_RUNTIME_LIBRARY "${CPLEX_STANDALONE_RUNTIME_LIBRARY_PATH}/cplex1262.dll")
endif()



# Check if everything was found and set CPLEX_STANDALONE_FOUND.
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    CplexStandalone
    REQUIRED_VARS CPLEX_STANDALONE_INCLUDE_DIRS CPLEX_STANDALONE_LIBRARIES
                  CPLEX_CONCERT_INCLUDE_DIRS CPLEX_CONCERT_LIBRARIES
                  CPLEX_ILO_LIBRARIES
)


set(CPLEX_STANDALONE_INCLUDE_DIRS ${CPLEX_STANDALONE_INCLUDE_DIRS} ${CPLEX_CONCERT_INCLUDE_DIRS})
set(CPLEX_STANDALONE_LIBRARIES ${CPLEX_CONCERT_LIBRARIES} ${CPLEX_ILO_LIBRARIES} ${CPLEX_STANDALONE_LIBRARIES})

mark_as_advanced(
    CPLEX_STANDALONE_INCLUDE_DIRS CPLEX_STANDALONE_LIBRARIES CPLEX_STANDALONE_LIBRARIES_COMMON CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX
    CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_32 CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_32
    CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE_64 CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG_64
    CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_RELEASE CPLEX_STANDALONE_LIBRARY_PATH_SUFFIX_DEBUG
    CPLEX_STANDALONE_LIBRARY_RELEASE CPLEX_STANDALONE_LIBRARY_DEBUG CPLEX_STANDALONE_RUNTIME_LIBRARY_PATH
    CPLEX_STANDALONE_VERSION CPLEX_STANDALONE_VERSION_MAJOR CPLEX_STANDALONE_VERSION_MINOR CPLEX_STANDALONE_VERSION_STR
    CPLEX_STANDALONE_VERSION_SUBMINOR
    CPLEX_CONCERT_LIBRARY_RELEASE CPLEX_CONCERT_LIBRARY_RELEASE32 CPLEX_CONCERT_LIBRARY_RELEASE64
    CPLEX_CONCERT_LIBRARY_DEBUG CPLEX_CONCERT_LIBRARY_DEBUG32 CPLEX_CONCERT_LIBRARY_DEBUG64
    CPLEX_CONCERT_INCLUDE_DIRS CPLEX_CONCERT_LIBRARIES
    CPLEX_ILO_LIBRARIES CPLEX_ILO_LIBRARY_RELEASE CPLEX_ILO_LIBRARY_RELEASE32 CPLEX_ILO_LIBRARY_RELEASE64
    CPLEX_ILO_LIBRARY_DEBUG CPLEX_ILO_LIBRARY_DEBUG32 CPLEX_ILO_LIBRARY_DEBUG64
)
